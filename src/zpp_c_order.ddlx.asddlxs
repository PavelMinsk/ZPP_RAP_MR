@Metadata.layer: #CORE

@UI: { 
       headerInfo: { 
                     typeName:       'Order',
                     typeNamePlural: 'Orders',
                     
                     title:        { label: 'Product',
                                     type:   #STANDARD,          
                                     value: 'Orderid'   },           
                     
                     description:  { value: 'ProductName',
                                     type: #STANDARD    }

                   },
       
       presentationVariant: [{  
                               sortOrder: [{ by: 'Orderid',  direction: #ASC }]
                            }]                            
     }
     
annotate entity zpp_c_order with
{
  @UI.facet: [
---------------------------------------------------------------------
//                       Header Facet Annotations                        
              {
                 id:              'HeaderGrossamount',
                 purpose:         #HEADER,
                 type:            #DATAPOINT_REFERENCE,
                 targetQualifier: 'Grossamount',
                 position: 10
              },
                            
---------------------------------------------------------------------            
//                       Object Page Tabs                                               
              {
                 id:                 'GeneralInformation',
                 type:               #COLLECTION,
                 label:              'General Information',
                 position:           10
              },
              {
                 id:                 'BusinessPartnerTab',
                 type:               #COLLECTION,
                 label:              'Business Partner',
                 position:           20
              },
              {
                 id:                 'AdminDataCollection',
                 type:               #COLLECTION,
                 label:              'Admin Data',
                 position:           30
              },
 --------------------------------------------------------------------             
//                      Field Groups              
              
              {
                 parentId:          'GeneralInformation',
                 id:                'BasicData',
                 purpose:           #STANDARD ,
                 type:              #FIELDGROUP_REFERENCE,
                 label:             'Basic Data',
                 position:          10,
                 targetQualifier:   'BasicData'
              },
              {
                 parentId:          'BusinessPartnerTab',
                 id:                'BusinessPartner',
                 purpose:           #STANDARD ,
                 type:              #FIELDGROUP_REFERENCE,
                 label:             'Business Partner',
                 position:          20,
                 targetQualifier:   'BusinessPartner'
              },
              {     
                 parentId:          'AdminDataCollection',     
                 id:                'AdminData',     
                 purpose:           #STANDARD ,     
                 type:              #FIELDGROUP_REFERENCE,     
                 label:             'Admin Data',     
                 position:          30,     
                 targetQualifier:   'AdminData'     
              } 
             ]     
 --------------------------------------------------------------------    
  
  
  @UI.hidden: true
  ProdUuid;
  @UI.hidden: true
  MrktUuid;
  @UI.hidden: true
  OrderUuid;
--------------------------------  
//                     BasicData  
  @UI: { 
         lineItem:       [{ position: 10, importance: #HIGH }],
         fieldGroup:     [{ position: 10, qualifier: 'BasicData' }]  
//         identification: [{ position: 10, label: 'Product Name' }]
       }
  ProductName;
  @UI: { 
         lineItem:       [{ position: 20, importance: #HIGH }],
         fieldGroup:     [{ position: 10, qualifier: 'BasicData' }],  
         selectionField: [{ position: 10 }]
//         identification: [{ position: 10, label: 'Order' }]
       }  
  @EndUserText.label: 'Order ID'
  Orderid;
  @UI: { 
         lineItem:     [{ position: 30, importance: #HIGH  }],
         fieldGroup:   [{ position: 20, qualifier: 'BasicData' }]  
       }
  Quantity;
  @UI: { 
         lineItem:   [{ position: 40, importance: #HIGH  }],
         fieldGroup: [{ position: 30, qualifier: 'BasicData' }]  
       }
  CalendarYear;
  @UI: { 
         lineItem:   [{ position: 50, importance: #HIGH  }],
         fieldGroup: [{ position: 40, qualifier: 'BasicData' }]  
       }
  DeliveryDate;
  @UI: { 
         lineItem:   [{ position: 60, importance: #HIGH }],
         fieldGroup: [{ position: 50, qualifier: 'BasicData' }]  
       }
  Netamount;
  @UI: { 
         lineItem:   [{ position: 70,    importance: #HIGH  }],
         dataPoint:  { title: 'Gross amount', qualifier: 'Grossamount' },
         fieldGroup: [{ position: 60,    qualifier: 'BasicData' }]  
       }
  Grossamount;
  @UI: { 
         lineItem:   [{ position: 80, importance: #HIGH  }],
         fieldGroup: [{ position: 70, qualifier: 'BasicData' }]
       }
  Amountcurr;
--------------------------------  
//               BusinessPartner  
  @UI: { 
         lineItem:       [{ position: 90, importance: #HIGH  }],
         fieldGroup:     [{ position: 10, qualifier: 'BusinessPartner' }],
         selectionField: [{ position: 20 }]
       }
  Busspartner;
  @UI: { 
//         lineItem:   [{ position: 100, importance: #HIGH }],
         fieldGroup: [{ position: 20, qualifier: 'BusinessPartner' }]
       }
  Busspartnername;
  @UI: { 
//         lineItem:   [{ position: 110, importance: #HIGH  }],
         fieldGroup: [{ position: 30, qualifier: 'BusinessPartner' }]
       }
  Busspartnergroup;
  
-------------------------------- 
//                     AdminData   
  @UI.fieldGroup: [{ position: 10, qualifier: 'AdminData' }]  
  CreatedBy;
  @UI.fieldGroup: [{ position: 20, qualifier: 'AdminData' }]  
  CreationTime;
  @UI.fieldGroup: [{ position: 30, qualifier: 'AdminData' }]  
  ChangedBy;
  @UI.fieldGroup: [{ position: 40, qualifier: 'AdminData' }]  
  ChangeTime;

}